plugins {
    id "com.diffplug.spotless" version "6.14.0"
}

allprojects {
    group 'me.darknet'
    version '2.0.0.1'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.diffplug.spotless'

    compileJava.options.encoding = "UTF-8"
    compileJava.options.incremental = true

    compileTestJava.options.encoding = "UTF-8"

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url 'https://jitpack.io' }
    }

    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }

        withSourcesJar()
    }

    spotless {
        java {
            def rootDir = rootProject.projectDir.toPath()

            importOrder("me.darknet", "", "\\#", "java|javax")
            removeUnusedImports()

            eclipse().configFile(rootDir.resolve("formatter.xml").toString())

            trimTrailingWhitespace()

        }
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    dependencies {
        // jetbrains annotations
        implementation 'org.jetbrains:annotations:22.0.0'
    }

    publishing {
        repositories {
            mavenLocal()
            maven {
                name = 'sources-repo'
                url = 'file:///tmp/sources-repo'
            }
        }
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }

    plugins.withType(JavaPlugin).configureEach {
        dependencies {
            testImplementation platform('org.junit:junit-bom:5.9.1')
            testImplementation 'org.junit.jupiter:junit-jupiter'
        }
    }

}